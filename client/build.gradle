import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.moowork.node" version "0.14"
}

node {
    version = '6.9.1'
    download = true
}

task installDependencies(type: NpmTask) {
    inputs.file "$projectDir/package.json"
    outputs.dir "$projectDir/node_modules"
    args = ["install"]
}

task build(type: NpmTask) {
    dependsOn installDependencies

    inputs.files fileTree(dir: projectDir, exclude: "build")
    // Output for react hot redeploy server was set to 'dev' folder.
    // In fact it never create this folder so simply use buildDir here.
    outputs.dir buildDir
    // webpack will be invoked with "npm run build" and
    // resulting bundle.js will be saved to build folder
    // by webpack itself (see webpack.config.js)
    args = ["run", "build"]
    doLast {
        def bundle = new File("$buildDir/bundle.js")
        if (!bundle.exists()) {
            throw new GradleException("$buildDir/bundle.js is not created")
        }
        bundle.renameTo("$buildDir/bundle-${version}.js")
        // As we want to have full frontend distribution in
        // build folder let's copy index.html and favicon
        copy {
            from ("src/index.html")
            into buildDir
            filter(ReplaceTokens, tokens: [version: version])
        }
        copy {
            from "src/favicon.png"
            into buildDir
        }
    }
}

task clean(type: Delete) {
    delete buildDir
}